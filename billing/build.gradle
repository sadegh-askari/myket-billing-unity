plugins {
    id "com.android.library"
    id "maven-publish"
}

android {
    compileSdk 34

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34

        def SDK_VERSION = 4
        buildConfigField("int", "SDK_VERSION", SDK_VERSION.toString())
        manifestPlaceholders["sdkVersion"] = SDK_VERSION.toString()
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    namespace 'ir.myket.billingclient'
    buildFeatures {
        aidl true
        buildConfig true
    }
}

dependencies {
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.core:core:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.gms:play-services-ads-identifier:18.1.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.findByName('release')
                artifact bundleReleaseAar
                groupId 'com.github.myketstore'
                artifactId 'myket-billing-client'
                version '1.17'

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each { dependency ->
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dependency.group)
                        dependencyNode.appendNode('artifactId', dependency.name)
                        dependencyNode.appendNode('version', dependency.version)
                    }
                }
            }
        }
    }
}